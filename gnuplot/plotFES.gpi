#!/usr/bin/gnuplot -c

# check if correct arguments were given
directory=ARG1
col=ARG2
if (directory eq "") {
  print "Error: Please specify directory as first argument"; exit }
if (col=="") {
  print "Error: Please specify column as second argument"; exit }
cd directory


# Terminal settings
#load "style/pdf.gpi"
#set output "~/FES.pdf"

# Set labels
set xlabel "Distance (nm)"
set ylabel "Average Free Energy (kT)"
set title "Db12 with linear interpolation"

# Set plot range and tic options
set yr [0:8]
set xr [0.2:0.8]
set xtics 0.1

# Define colors
colors = "#e41a1c #377eb8 #4daf4a #984ea3 #ff7f00 #ffff33 #a65628"
set for [i=1:words(colors)] linetype i lw 3 lc rgb word(colors, i)

set key top right horizontal sample 1.

titlefunc(secs) = (int(secs) < 1000 ? secs." ps" : sprintf("%d ns", int(secs)/1000))
kT = 2.494339


set macros  # needed for the column argument
columns = "using 1:($".col."/kT)"

times = "500 1000 5000 8000 10000"
pl for [time in times] sprintf("fes.b1.iter-%s.data", time) @columns w l title titlefunc(time)
