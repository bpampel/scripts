#!/usr/bin/gnuplot -c

# check if dir was given as argument
directory=ARG1
if (directory eq "") {
  print "Error: Please specify directory as first argument"
  exit
}
cd directory

# Terminal settings
#load "style/pdf.gpi"
#set output "~/FESwithErrors.pdf"

# Set labels
set xlabel "Distance (nm)"
set ylabel "Free Energy (k_{B}T)"
set notitle

# Set plot range and tic options
set yr [0:7]
set xr [0.25:0.75]
#set xtics 0.1
set ytics 2

set key top right vertical sample 1.

set style fill transparent solid 0.3 noborder

titlefunc(secs) = (int(secs) < 1000 ? secs." ps" : sprintf("%d ns", int(secs)/1000))
kT = 2.494339


times = "500 1000 5000 10000"
pl for [time in times]\
    sprintf("fes.b1.iter-%s.data", time) u 1:(($2-$5)/kT):(($2+$5)/kT) w filledcurves below notitle,\
   for [i=1:words(times)]\
    sprintf("fes.b1.iter-%s.data", word(times, i)) u 1:($2/kT) w l lt i title titlefunc(word(times,i))
