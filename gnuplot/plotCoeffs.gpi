# switch to folder from first argument
cd ARG1

# Terminal settings
#set terminal pdfcairo color solid size 17.2 cm, 12.9 cm font ",18"
#set output "~/Coeffs.pdf"
#set border lw 1.5

# Set labels
set xlabel "Time (ns)"
set ylabel "Coefficients (kT)"
set title "Db12 with linear interpolation"

# Set plot range and tic options
#set xr [0:10]
colors = "#e41a1c #377eb8 #4daf4a #984ea3 #ff7f00 #ffff33 #a65628"
set for [i=1:words(colors)] linetype i lw 3 lc rgb word(colors, i)

set key top right horizontal

coeffs_stride=10
ns_per_iteration=0.001
kT=2.494339

filename='coeffs.data'
coefflabel(n) = sprintf("Î±_{%d}", n)
coeffdata(n,m)=sprintf("< awk '$1==%d {print $%d}' %s", n, m, filename)

# variables for multiplot
coeffs_per_plot = 6
coeffnum = 35

do for [p=0:(coeffnum/coeffs_per_plot)]{
  pl for [i=(p*coeffs_per_plot):((1+p)*coeffs_per_plot)-1]\
    coeffdata(i,2) u ($0*coeffs_stride*ns_per_iteration):($1/kT) w l title coefflabel(i),\
     for [i=(p*coeffs_per_plot):((1+p)*coeffs_per_plot)-1]\
      coeffdata(i,3) u ($0*coeffs_stride*ns_per_iteration):($1/kT) w l lt (i%coeffs_per_plot+1) lw 1 notitle
  pause -1
}
